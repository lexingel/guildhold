class_name DragIcon
extends TextureRect
## A TextureRect that starts a Control drag when picked up, carrying whatever
## Variant is set as `drag_payload` to any DropButton it's released over (see
## Main.gd's _action_slot `drop_target` param). Returns null (no drag) when
## drag_payload is unset, so a plain _icon()-built TextureRect can be swapped
## for this without becoming draggable by accident.

var drag_payload: Variant = null

func _get_drag_data(_at_position: Vector2) -> Variant:
	if drag_payload == null:
		return null
	var preview := TextureRect.new()
	preview.texture = texture
	preview.custom_minimum_size = custom_minimum_size
	preview.size = size
	preview.stretch_mode = TextureRect.STRETCH_KEEP_ASPECT_CENTERED
	preview.texture_filter = CanvasItem.TEXTURE_FILTER_NEAREST
	preview.modulate = Color(1, 1, 1, 0.85)
	set_drag_preview(preview)
	return drag_payload


## Called on a plain click (pressed and released without moving): the icon
## stays draggable and still works as a button.
var clicked: Callable = Callable()
var _press_at := Vector2(-1, -1)


func _gui_input(event: InputEvent) -> void:
	if not clicked.is_valid() or not (event is InputEventMouseButton) or event.button_index != MOUSE_BUTTON_LEFT:
		return
	if event.pressed:
		_press_at = event.position
	elif _press_at.x >= 0.0 and event.position.distance_to(_press_at) < 6.0:
		_press_at = Vector2(-1, -1)
		clicked.call()


## BBCode tooltips render as item cards (see RichTip).
func _make_custom_tooltip(for_text: String) -> Object:
	return RichTip.card(for_text)


## Builds the tooltip on hover instead of with the icon: an item card weighs
## the swap on the hero (Power with and without it), ~2 ms a tile (0.69.1).
var tooltip_fn := Callable()


func _get_tooltip(_at_position: Vector2) -> String:
	return str(tooltip_fn.call()) if tooltip_fn.is_valid() else tooltip_text
